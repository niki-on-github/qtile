# Maintainer: none
# Contributor: none

pkgname=qtile-git
pkgver=0.21.0.r17.ga0889917
pkgrel=1
pkgdesc="A full-featured, pure-Python tiling window manager. (git version)"
arch=('x86_64')
url="http://www.qtile.org"
license=('MIT')

_pywlroots_requirement="0.15.12"

# Technically the X-related dependencies are *not* required, if the user only
# wants to use the Wayland backend. However this would cause disruption, so
# let's hold off on changing that for now.
depends=(
  'gdk-pixbuf2'
  'glibc'
  'pango'
  'python-cairocffi'
  'python-cffi'
  'python-xcffib'
)

makedepends=(
  'git'
  'python-setuptools'
  'python-setuptools-scm'
  'libpulse'
  "python-pywlroots>=$_pywlroots_requirement" 'python-pywlroots<0.16.0'
)
checkdepends=(
  'dbus'
  'graphviz'
  'gtk3'
  'imagemagick'
  'libnotify'
  'librsvg'
  'mypy'
  'python-bowler'
  'python-dbus-next'
  'python-gobject'
  'python-pytest'
  "python-pywlroots>=$_pywlroots_requirement" 'python-pywlroots<0.16.0'
  'python-xdg'
  'python-xvfbwrapper'
  'wlroots'
  'xorg-server-xephyr'
  'xorg-xrandr'
  'xorg-xwayland'
)
optdepends=(
  'alsa-utils: volume widget'
  'canto-daemon: canto widget'
  'cmus: cmus widget'
  'jupyter_console: interaction with qtile via Jupyter'
  'khal: khal_calendar widget'
  'libpulse: for pulse_volume and pulseaudio_ffi widget'
  'librsvg: for SVG support in some widgets widgets or wallpapers'
  'lm_sensors: sensors widget'
  'moc: moc widget'
  'python-dbus-next: for utils, notifications and several widgets'
  'python-iwlib: wlan widget'
  'python-keyring: imapwidget widget'
  'python-mpd2: mpd2widget widget'
  'python-psutil: graph, net and memory widget'
  'python-pywlroots: Wayland backend'
  'xorg-xwayland: Wayland backend'
  'python-setproctitle: change process name to qtile'
  'python-xdg: launchbar widget'
)
provides=('qtile')
conflicts=('qtile')
install=${pkgname}.install
source=('git+https://github.com/niki-on-github/qtile.git#branch=testing')
md5sums=('SKIP')

pkgver() {
  cd qtile
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

build() {
  cd qtile
  python setup.py build
  ./scripts/ffibuild
}

check() {
  cd qtile
  export LC_TYPE=en_US.UTF-8
  # export MYPYPATH="$PWD:$PWD/stubs"
  # mypy-based tests are ignored until I figure out how to fix them
  # Plus they won't change from merge to package
  #pytest -vv --backend x11 --backend wayland \
  #  --ignore test/test_check.py --ignore test/test_migrate.py test
}

package() {
  cd qtile
  python setup.py install --skip-build --optimize=1 --root="$pkgdir"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 CHANGELOG README.rst libqtile/resources/default_config.py \
    -t "${pkgdir}/usr/share/doc/$pkgname/"
  install -vDm 644 resources/qtile.desktop -t "$pkgdir/usr/share/xsessions/"
  install -vDm 644 resources/qtile-wayland.desktop -t "$pkgdir/usr/share/wayland-sessions/"
}
